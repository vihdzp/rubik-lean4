import Mathlib.Tactic.DeriveFintype
import Mathlib.Data.Fintype.Perm
import Mathlib.Data.Fintype.Prod
import Mathlib.Data.Prod.Lex

/-!
Defines the geometric primitives required to construct a Rubik's cube.

Rather than trying to construct a Rubik's cube as a concrete object living in `‚Ñù¬≥`, we speak of each
piece being oriented in a certain way with respect to the cartesian axes. For instance, the
white-red edge piece in a solved cube is oriented upwards and to the right.

We first define the primitive `Axis` representing the X, Y, and Z axes in 3D space. An orientation
consists of a "sign" (`Bool`) alongside an axis, for a total of 6 orientations of a face in a cube.

Since we consider the centers in a Rubik's cube to be fixed, we can readily identify each color with
a unique orientation.

Two orientations define an edge whenever they are `IsAdjacent`, i.e. their axes are distinct. Three
orientations define a corner whenever they are `IsAdjacent‚ÇÉ`, i.e. pairwise adjacent. It's
physically impossible to change the chirality of a corner, which we encode by adding the requirement
on `IsAdjacent‚ÇÉ` that the orientations form a positively oriented basis.

Since our types are finite and readily allow for computation, most proofs here are done with
`by decide`.
-/

/-- A Cartesian axis in 3D space. -/
inductive Axis : Type
  /-- The `x` or left-right axis. -/
  | x : Axis
  /-- The `y` or bottom-top axis. -/
  | y : Axis
  /-- The `z` or back-front axis. -/
  | z : Axis

deriving instance DecidableEq, Fintype for Axis

namespace Axis

variable {a b c : Axis}

instance : Repr Axis := ‚ü®fun e _ ‚Ü¶ Std.Format.text <| match e with
  | Axis.x => "X"
  | Axis.y => "Y"
  | Axis.z => "Z"
‚ü©

protected theorem card : Fintype.card Axis = 3 :=
  rfl

/-- Permutes the `x`, `y`, `z` axes in cyclic order. -/
def rotate : Axis ‚Üí Axis
  | Axis.x => Axis.y
  | Axis.y => Axis.z
  | Axis.z => Axis.x

theorem rotate_ne : ‚àÄ a : Axis, a.rotate ‚â† a := by
  decide

@[simp]
theorem rotate_inj : ‚àÄ {a b : Axis}, a.rotate = b.rotate ‚Üî a = b := by
  decide

/-- Given two distinct axes, returns the third. If both axes are equal, we just return it. -/
def other : Axis ‚Üí Axis ‚Üí Axis
  | Axis.x, Axis.y => Axis.z
  | Axis.x, Axis.z => Axis.y
  | Axis.y, Axis.x => Axis.z
  | Axis.y, Axis.z => Axis.x
  | Axis.z, Axis.x => Axis.y
  | Axis.z, Axis.y => Axis.x
  | Axis.x, Axis.x => Axis.x
  | Axis.y, Axis.y => Axis.y
  | Axis.z, Axis.z => Axis.z

@[simp]
theorem other_self : ‚àÄ a, other a a = a := by
  decide

@[simp]
theorem other_eq_left_iff : ‚àÄ {a b : Axis}, other a b = a ‚Üî a = b := by
  decide

@[simp]
theorem other_eq_right_iff : ‚àÄ {a b : Axis}, other a b = b ‚Üî a = b := by
  decide

theorem other_eq_iff : ‚àÄ {a b c : Axis}, a ‚â† b ‚Üí (other a b = c ‚Üî c ‚â† a ‚àß c ‚â† b) := by
  decide

theorem other_eq (h‚ÇÅ : a ‚â† b) (h‚ÇÇ : c ‚â† a) (h‚ÇÉ : c ‚â† b) : other a b = c :=
  (other_eq_iff h‚ÇÅ).2 ‚ü®h‚ÇÇ, h‚ÇÉ‚ü©

theorem other_ne_iff (h : a ‚â† b) : other a b ‚â† c ‚Üî c = a ‚à® c = b := by
  rw [‚Üê not_iff_not, not_ne_iff, other_eq_iff h, not_or]

theorem other_comm : ‚àÄ a b : Axis, other a b = other b a := by
  decide

theorem other_ne_left (h : a ‚â† b) : other a b ‚â† a :=
  ((other_eq_iff h).1 rfl).1

theorem other_ne_right (h : a ‚â† b) : other a b ‚â† b :=
  ((other_eq_iff h).1 rfl).2

@[simp]
theorem other_other_left : ‚àÄ {a b : Axis}, other (other a b) a = b := by
  decide

@[simp]
theorem other_other_right : ‚àÄ {a b : Axis}, other (other a b) b = a := by
  decide

@[simp]
theorem other_other_left' : other a (other a b) = b := by
  rw [other_comm, other_other_left]

@[simp]
theorem other_other_right' : other b (other a b) = a := by
  rw [other_comm, other_other_right]

@[simp]
theorem other_inj_left : ‚àÄ {a b c : Axis}, other c a = other c b ‚Üî a = b := by
  decide

@[simp]
theorem other_inj_right : other a c = other b c ‚Üî a = b := by
  rw [other_comm, @other_comm b, other_inj_left]

/-- An arbitrary assignment of naturals for the axes, used to define the linear order instance. -/
def toNat : Axis ‚Üí ‚Ñï
  | Axis.x => 0
  | Axis.y => 1
  | Axis.z => 2

theorem toNat_injective : Function.Injective toNat := by
  decide

/-- An "arbitrary" computable linear order. -/
instance : LinearOrder Axis :=
  LinearOrder.lift' _ toNat_injective

end Axis

/-- One of six possible orientations for a face of a Rubik's cube, represented as `Bool √ó Axis`.

We employ the convention that the sign argument is `true` for the front, right, and up orientations.

This type will also be used for the colors in a Rubik's cube, using the following convention:

* Red = Right
* White = Up
* Green = Front
* Orange = Left
* Yellow = Down
* Blue = Back
-/
def Orientation : Type := Bool √ó Axis

namespace Orientation

variable {a b c r : Orientation}

instance : Inhabited Orientation :=
  ‚ü®(true, Axis.x)‚ü©

instance decEq : DecidableEq Orientation :=
  inferInstanceAs (DecidableEq (Bool √ó Axis))

/-- An "arbitrary" computable linear order. -/
instance : LinearOrder Orientation :=
  inferInstanceAs (LinearOrder (Lex (Bool √ó Axis)))

instance : Repr Orientation := ‚ü®fun e _ ‚Ü¶ Std.Format.text <| match e with
  | (true, Axis.x) => "R"
  | (true, Axis.y) => "U"
  | (true, Axis.z) => "F"
  | (false, Axis.x) => "L"
  | (false, Axis.y) => "D"
  | (false, Axis.z) => "B"
‚ü©

/-- The color represented by an orientation, as a Unicode square. -/
def color : Orientation ‚Üí String
  | (true, Axis.x) => "üü•"
  | (true, Axis.y) => "‚¨ú"
  | (true, Axis.z) => "üü©"
  | (false, Axis.x) => "üüß"
  | (false, Axis.y) => "üü®"
  | (false, Axis.z) => "üü¶"

instance : HAppend Std.Format Orientation Std.Format :=
  ‚ü®fun s a ‚Ü¶ s ++ a.color‚ü©

instance instFintype : Fintype Orientation :=
  inferInstanceAs (Fintype (Bool √ó Axis))

protected theorem card : Fintype.card Orientation = 6 :=
  rfl

/-- Right orientation or red color. -/
def R : Orientation := (true, Axis.x)
/-- Up orientation or white color. -/
def U : Orientation := (true, Axis.y)
/-- Front orientation or green color. -/
def F : Orientation := (true, Axis.z)

/-- Left orientation or orange color. -/
def L : Orientation := (false, Axis.x)
/-- Down orientation or yellow color. -/
def D : Orientation := (false, Axis.y)
/-- Back orientation or blue color. -/
def B : Orientation := (false, Axis.z)

/-- The sign (positive or negative) corresponding to the orientation. -/
def sign (a : Orientation) : Bool :=
  a.1

@[simp]
theorem sign_mk (b : Bool) (a : Axis) : sign (b, a) = b :=
  rfl

/-- The Cartesian axis corresponding to the orientation. -/
def axis (a : Orientation) : Axis :=
  a.2

@[simp]
theorem axis_mk (b : Bool) (a : Axis) : axis (b, a) = a :=
  rfl

@[ext]
theorem ext (h‚ÇÅ : sign a = sign b) (h‚ÇÇ : axis a = axis b) : a = b :=
  Prod.ext h‚ÇÅ h‚ÇÇ

/-- The opposite of an orientation, i.e. the orientation with the same axis and opposite sign. -/
instance : Neg Orientation :=
  ‚ü®fun a ‚Ü¶ (!a.1, a.2)‚ü©

instance : InvolutiveNeg Orientation :=
  ‚ü®fun _ ‚Ü¶ ext (Bool.not_not _) rfl‚ü©

@[simp]
theorem neg_mk (b : Bool) (a : Axis) : instNeg.neg (b, a) = (!b, a) :=
  rfl

@[simp]
theorem sign_neg (a : Orientation) : (-a).sign = !a.sign :=
  rfl

@[simp]
theorem axis_neg (a : Orientation) : (-a).axis = a.axis :=
  rfl

/-- Two orientations are adjacent when they have distinct axes. -/
def IsAdjacent (a b : Orientation) : Prop :=
  a.axis ‚â† b.axis

instance IsAdjacent.decRel : DecidableRel IsAdjacent :=
  inferInstanceAs (‚àÄ a b : Orientation, Decidable (a.axis ‚â† b.axis))

@[simp]
theorem neg_isAdjacent : IsAdjacent (-a) b ‚Üî IsAdjacent a b :=
  Iff.rfl

@[simp]
theorem isAdjacent_neg : IsAdjacent a (-b) ‚Üî IsAdjacent a b :=
  Iff.rfl

theorem IsAdjacent.ne (h : IsAdjacent a b) : a ‚â† b := by
  rintro rfl
  exact h rfl

theorem isAdjacent_comm : IsAdjacent a b ‚Üî IsAdjacent b a :=
  ne_comm

alias ‚ü®IsAdjacent.symm, _‚ü© := isAdjacent_comm

/-- Given two adjacent orientations, returns the "cross product", i.e. the orientation `c` adjacent
to both, such that `(a, b, c)` is oriented as the standard basis. -/
def cross (a b : Orientation) : Orientation :=
  ((a.axis.rotate == b.axis) == (a.sign == b.sign), a.axis.other b.axis)

@[simp]
theorem sign_cross (a b : Orientation) :
    (cross a b).sign = ((a.axis.rotate == b.axis) == (a.sign == b.sign)) :=
  rfl

@[simp]
theorem axis_cross (a b : Orientation) : (cross a b).axis = a.axis.other b.axis :=
  rfl

theorem IsAdjacent.cross_left (h : IsAdjacent a b) : IsAdjacent (cross a b) a :=
  Axis.other_ne_left h

theorem IsAdjacent.cross_right (h : IsAdjacent a b) : IsAdjacent (cross a b) b :=
  Axis.other_ne_right h

@[simp]
theorem cross_neg_left : ‚àÄ (a b : Orientation), cross (-a) b = -cross a b := by
  decide

@[simp]
theorem cross_neg_right : ‚àÄ (a b : Orientation), cross a (-b) = -cross a b := by
  decide

theorem cross_asymm : ‚àÄ {a b}, IsAdjacent a b ‚Üí cross a b = - cross b a := by
  decide

@[simp]
theorem cross_inj_left : ‚àÄ {a b c}, cross a c = cross b c ‚Üî a = b := by
  decide

@[simp]
theorem cross_inj_right : ‚àÄ {a b c}, cross a b = cross a c ‚Üî b = c := by
  decide

@[simp]
theorem cross_cross_left : ‚àÄ (a b), cross (cross a b) a = b := by
  decide

@[simp]
theorem cross_cross_right : ‚àÄ (a b), cross b (cross a b) = a := by
  decide

theorem cross_cross_left' (h : IsAdjacent a b) : cross a (cross a b) = -b := by
  rw [cross_asymm h, cross_neg_right, cross_cross_right]

theorem cross_cross_right' (h : IsAdjacent a b) : cross (cross a b) b = -a := by
  rw [cross_asymm h, cross_neg_left, cross_cross_left]

/-- Take a piece with stickers on orientations `a ‚â† r`, and perform a **counterclockwise** rotation
in orientation `r`. This function returns the new orientation of the sticker with orientation `a`.

For instance, `rotate U F = L` since performing `F'` sends the upper-front corner to the left-front
one.

The reason this is inverted is so that
`(cube.rotate r).edge a b = Cube.edge (a.rotate r) (b.rotate r)`. -/
def rotate (a r : Orientation) : Orientation :=
  if r.axis = a.axis then a else cross r a

theorem rotate_of_eq (h : r.axis = a.axis) : a.rotate r = a :=
  dif_pos h

theorem rotate_of_ne (h : r.axis ‚â† a.axis) : a.rotate r = cross r a :=
  dif_neg h

@[simp]
theorem rotate_neg : rotate (-a) r = -rotate a r := by
  by_cases h : r.axis = a.axis
  ¬∑ rwa [rotate_of_eq h, rotate_of_eq]
  ¬∑ rwa [rotate_of_ne h, rotate_of_ne, cross_neg_right]

@[simp]
theorem rotate_inj : ‚àÄ {a b r : Orientation}, rotate a r = rotate b r ‚Üî a = b := by
  decide

theorem isAdjacent_rotate : ‚àÄ {a b r : Orientation},
    IsAdjacent (a.rotate r) (b.rotate r) ‚Üî IsAdjacent a b := by
  decide

theorem IsAdjacent.rotate (h : IsAdjacent a b) (r : Orientation) :
    IsAdjacent (a.rotate r) (b.rotate r) :=
  isAdjacent_rotate.2 h

/-- A predicate for three pairwise adjacent orientations, oriented as the standard basis.

The orientation condition is required, since it's not physically possible to exchange two pieces in
a corner without dissassembling it. -/
@[pp_nodot]
def IsAdjacent‚ÇÉ (a b c : Orientation) : Prop :=
  IsAdjacent a b ‚àß c = cross a b

instance IsAdjacent‚ÇÉ.decRel : ‚àÄ a b c : Orientation, Decidable (IsAdjacent‚ÇÉ a b c) :=
  inferInstanceAs (‚àÄ a b c, Decidable (IsAdjacent a b ‚àß c = cross a b))

theorem IsAdjacent‚ÇÉ.isAdjacent (h : IsAdjacent‚ÇÉ a b c) : IsAdjacent a b :=
  h.1

theorem IsAdjacent‚ÇÉ.eq_cross (h : IsAdjacent‚ÇÉ a b c) : c = cross a b :=
  h.2

theorem IsAdjacent.isAdjacent‚ÇÉ (h : IsAdjacent a b) : IsAdjacent‚ÇÉ a b (cross a b) :=
  ‚ü®h, rfl‚ü©

theorem IsAdjacent‚ÇÉ.congr {c‚ÇÅ c‚ÇÇ : Orientation}
    (h‚ÇÅ : IsAdjacent‚ÇÉ a b c‚ÇÅ) (h‚ÇÇ : IsAdjacent‚ÇÉ a b c‚ÇÇ) : c‚ÇÅ = c‚ÇÇ :=
  h‚ÇÅ.2.trans h‚ÇÇ.2.symm

theorem isAdjacent‚ÇÉ_cyclic : IsAdjacent‚ÇÉ a b c ‚Üî IsAdjacent‚ÇÉ b c a := by
  constructor <;>
  rintro ‚ü®h, rfl‚ü©
  ¬∑ exact ‚ü®(h.cross_right).symm, (cross_cross_right _ _).symm‚ü©
  ¬∑ exact ‚ü®h.cross_left, (cross_cross_left _ _).symm‚ü©

alias ‚ü®IsAdjacent‚ÇÉ.cyclic, _‚ü© := isAdjacent‚ÇÉ_cyclic

theorem IsAdjacent‚ÇÉ.ne (h : IsAdjacent‚ÇÉ a b c) : a ‚â† b ‚àß b ‚â† c ‚àß c ‚â† a :=
  ‚ü®h.isAdjacent.ne, h.cyclic.isAdjacent.ne, h.cyclic.cyclic.isAdjacent.ne‚ü©

theorem cross_rotate : ‚àÄ {a b r : Orientation},
    IsAdjacent a b ‚Üí cross (a.rotate r) (b.rotate r) = (cross a b).rotate r := by
  decide

theorem isAdjacent‚ÇÉ_rotate :
    IsAdjacent‚ÇÉ (a.rotate r) (b.rotate r) (c.rotate r) ‚Üî IsAdjacent‚ÇÉ a b c := by
  constructor
  ¬∑ rintro ‚ü®h, hr‚ü©
    have H := isAdjacent_rotate.1 h
    rw [cross_rotate H, rotate_inj] at hr
    exact ‚ü®H, hr‚ü©
  ¬∑ rintro ‚ü®h, rfl‚ü©
    exact ‚ü®h.rotate r, (cross_rotate h).symm‚ü©

theorem IsAdjacent‚ÇÉ.rotate (h : IsAdjacent‚ÇÉ a b c) (r : Orientation) :
    IsAdjacent‚ÇÉ (a.rotate r) (b.rotate r) (c.rotate r) :=
  isAdjacent‚ÇÉ_rotate.2 h

end Orientation
